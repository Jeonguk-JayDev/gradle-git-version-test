plugins {
    id 'com.gladed.androidgitversion' version '0.4.13'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

androidGitVersion {
    codeFormat 'MNNPPBB'
    commitHashLength = 8
    format '%tag%%-branch%'
    hideBranches = [ 'master', 'prod' ]
    prefix('v')
    onlyIn('app')
    tagPattern(/^$prefix[0-9]+.*/)
    untrackedIsDirty = true
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.jaydev.git.myapplication"
        minSdkVersion 21
        targetSdkVersion 30
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            versionNameSuffix = "-SNAPSHOT"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "app-${variant.name}-v${versionName}-c${versionCode}.apk"
        }
    }
    flavorDimensions "api"
    productFlavors {
        dev {
            dimension "api"
            versionNameSuffix "-${androidGitVersion.code() % 100}-dev"
        }
        staging {
            dimension "api"
            versionNameSuffix "-${androidGitVersion.code() % 100}-stg"
        }
        prod {
            dimension "api"
            versionNameSuffix "-${androidGitVersion.code() % 100}-prd"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}